buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.7.10-1.0"
def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER) {
    version += "." + ENV.DRONE_BUILD_NUMBER
} else {
    version += ".local"
}

group= "de.take_weiland.mods.biomeid" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BiomeIDFixer"

minecraft {
    version = "1.7.10-10.13.2.1286"
    runDir = "eclipse"

    replaceIn "BiomeIDFixer.java"
    replace "@VERSION@", project.version
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'de.take_weiland.mods.biomeid.BiomeIDFixerLoader'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

tasks.build.dependsOn('sourceJar', 'deobfJar')

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}
